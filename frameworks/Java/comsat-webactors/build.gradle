// Capsule plugin
plugins {
    id "us.kirchmeier.capsule" version "1.0.0"
}

apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = '1.0'

ext.jacksonVer = '2.5.4'
ext.quasarVer  = '0.7.3'
ext.comsatVer  = '0.5.0'

[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

repositories {
    // mavenLocal()
    mavenCentral()
}

configurations {
    quasar
}

configurations.capsule.dependencies.clear()

dependencies {
    compile "co.paralleluniverse:quasar-core:$quasarVer:jdk8"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVer"
    compile "co.paralleluniverse:comsat-actors-api:$comsatVer"
    compile "co.paralleluniverse:comsat-actors-servlet:$comsatVer"
    compile "co.paralleluniverse:comsat-actors-undertow:$comsatVer"
    compile "co.paralleluniverse:comsat-actors-netty:$comsatVer"
    compile "co.paralleluniverse:comsat-test-utils:$comsatVer"

    capsule "co.paralleluniverse:capsule:1.0"

    quasar "co.paralleluniverse:quasar-core:$quasarVer:jdk8"
}

task capsule(type: FatCapsule) {
    applicationClass "servers.Main"

    capsuleManifest {
        javaAgents = [configurations.quasar.iterator().next().getName()]

        mode('undertow') {
            systemProperties['serverClass'] = 'servers.UndertowServer'
        }

        mode('netty') {
            systemProperties['serverClass'] = 'servers.NettyServer'
        }

        mode('servlet-undertow') {
            systemProperties['serverClass'] = 'servers.ServletServer'
            systemProperties['servletServerClass'] = 'co.paralleluniverse.embedded.containers.UndertowServer'
        }

        mode('servlet-jetty') {
            systemProperties['serverClass'] = 'servers.ServletServer'
            systemProperties['servletServerClass'] = 'co.paralleluniverse.embedded.containers.JettyServer'
        }

        mode('servlet-tomcat') {
            systemProperties['serverClass'] = 'servers.ServletServer'
            systemProperties['servletServerClass'] = 'co.paralleluniverse.embedded.containers.TomcatServer'
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}
